plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id "com.diffplug.spotless" version "8.2.1"
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

springBoot {
    mainClass = "ci.CiApplication"
}

configurations {
  checkstyle {
    exclude group: "com.google.collections", module: "google-collections"
  }
}

sourceSets {
  main {
    resources {
      srcDir 'src/main/webapp'
    }
  }
}

dependencies {
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
    implementation 'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
    }
}

spotless {
    java {
        target 'src/**/*.java'

        // Google Java Format
        googleJavaFormat("1.33.0")
        // Other checks
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

checkstyle {
    toolVersion = "10.12.5"
    configFile = file("config/checkstyle/checkstyle.xml")
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
    }
}

application {
    mainClass = 'ci.CiApplication'
}

tasks.register("format") {
    group = "formatting"
    description = "Auto-formats code (Google Java Format) + other cleanups"
    dependsOn("spotlessApply")
}

tasks.register("formatCheck") {
    group = "formatting"
    description = "Checks formatting without changing files"
    dependsOn("spotlessCheck")
}

tasks.register("ci") {
    group = "verification"
    description = "Format, style-check, build, and test."

    // Apply Spotless
    dependsOn("spotlessApply")

    // Verify formating and stylecheck
    dependsOn("check")

    // run all tests
    dependsOn("test")

    // ensure the build works
    dependsOn("build")

}

// Make sure webapp is included
tasks.named("bootRun") {
  sourceResources sourceSets.main
}

// Defines the order if several tasks are called 
tasks.named("spotlessCheck").configure { mustRunAfter("spotlessApply") }
tasks.named("check").configure { mustRunAfter("spotlessApply") }
tasks.named("build").configure { mustRunAfter("check") }

check.dependsOn("spotlessCheck")
check.dependsOn("checkstyleMain")
check.dependsOn("checkstyleTest")